apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'
//apply from: rootProject.file("plugins/android-paperwork.gradle")

def libraries = rootProject.ext.libraries
def versions = rootProject.ext.versions

repositories {
    // required by crashlytics
    maven {
        url("https://maven.fabric.io/public")
    }

    // used by sdk
    maven {
        url("https://oss.sonatype.org/content/repositories/snapshots")
    }
    mavenCentral()
}

android {

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileSdkVersion versions.sdk
    buildToolsVersion versions.tools
    defaultConfig {
        applicationId "com.dhis2"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.sdk
        versionCode versions.vCode
        versionName versions.vName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        setProperty("archivesBaseName", "dhis2-v" + versionName)

        buildConfigField "String", "SDK_VERSION", "\"" + libraries.dhis2sdk + "\""
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    configurations.all*.exclude module: 'jsr305'


    packagingOptions {
        // excluding duplicate license files
        // from jackson modules
        exclude "META-INF/LICENSE"

        // excluding duplicate rxjava properties file
        // see https://github.com/ReactiveX/RxJava/issues/4445#issuecomment-243591879
        exclude 'META-INF/rxjava.properties'
    }

    signingConfigs {
        release {
            storeFile file('dhis_keystore.jks')
            storePassword KEYSTORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEYSTORE_PASSWORD
        }
    }

    buildTypes {
        debug {
            // custom application suffix which allows to
            // install debug and release builds at the same time
            applicationIdSuffix = ".debug"

            // jacoco test coverage reports both for
            // androidTest and test source sets
            testCoverageEnabled = false

            // Disable fabric build ID generation for debug builds
//            ext.enableCrashlytics = false

            // Using dataentry.jks to sign debug build type.
            signingConfig = signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ext.enableCrashlytics = true
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/rxjava.properties'
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    configurations.all {
        resolutionStrategy.force 'junit:junit:4.12'
    }
}

dependencies {

    implementation project(":customFab")
    implementation project(":dhis2-rule-engine")

    //region SUPPORT LIBRARIES
    implementation "com.android.support:appcompat-v7:${libraries.support}"
    implementation "com.android.support:support-annotations:${libraries.support}"
    implementation "com.android.support:cardview-v7:${libraries.support}"
    implementation "com.android.support:design:${libraries.support}"
    implementation "com.android.support:support-v4:${libraries.support}"

    implementation "com.android.support:multidex:${libraries.multidex}"
    implementation "com.android.support.constraint:constraint-layout:${libraries.constraintlayout}"
    //endregion

    //region DHIS2 LIBRARIES
    implementation("org.hisp.dhis:android-core:${libraries.dhis2sdk}") {
        exclude group: "org.hisp.dhis", module: "core-rules"
    }

   /* implementation("org.hisp.dhis:android-rules:${libraries.dhis2sdk}") {
        exclude group: "com.google.code.findbugs", module: "jsr305"
        exclude group: "org.hisp.dhis", module: "core-rules"
    }*/

//    implementation"org.hisp.dhis.rules:rule-engine:${libraries.ruleEngine}"
    //endregion

    //region UI LIBRARIES
    //TreeView
    implementation "com.github.bmelnychuk:atv:${libraries.atv}"
    //FAB
    implementation "com.github.dekoservidoni:omfm:${libraries.omfm}"
    //FLEXBOX
    implementation "com.google.android:flexbox:${libraries.flexbox}"
    //TABLEVIEW
    implementation "com.evrencoskun.library:tableview:${libraries.tableview}"
    //PIN LOCK
    implementation 'com.andrognito.pinlockview:pinlockview:2.1.0'
    //endregion

    //region RXJAVA
    implementation "io.reactivex.rxjava2:rxandroid:${libraries.rxandroid}"
    implementation "io.reactivex.rxjava2:rxjava:${libraries.rxjava}"
    implementation "com.github.akarnokd:rxjava2-interop:${libraries.rxjavainteroperop}"
    //rxbinding
    implementation "com.jakewharton.rxbinding2:rxbinding:${libraries.rxbindings}"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:${libraries.rxbindings}"
    //endregion

    //region DAGGER
    implementation "com.google.dagger:dagger:${libraries.dagger}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    annotationProcessor "com.google.dagger:dagger-compiler:${libraries.dagger}"
    //endregion

    //region GOOGLE AUTO VALUE
    // Google's auto-value. Annotations are used only during compilation time, as well as
    // annotation processor which hoops up into compilation process
    compileOnly "com.google.auto.value:auto-value:${libraries.autovalue}"
    annotationProcessor "com.google.auto.value:auto-value:${libraries.autovalue}"

    // Extension which generates mappers for work with cursors
    annotationProcessor "com.gabrielittner.auto.value:auto-value-cursor:${libraries.autovaluecursor}"
    annotationProcessor "com.ryanharter.auto.value:auto-value-parcel:${libraries.autovalueparcel}"
    //endregion

    //region BRITEDATABASE
    //square
    implementation "com.squareup.sqlbrite3:sqlbrite:${libraries.sqlbrite}"
    implementation "com.github.lykmapipo:sqlbrite-migrations:v1.0.1"
    //endregion

    //region PLAY SERVICES
    //VISION (QR SCANNER)
    implementation("com.google.android.gms:play-services-vision:${libraries.gmsVersion}") {
        force = true
    }
    //LOCATION
    implementation("com.google.android.gms:play-services-location:${libraries.gmsVersion}")
    implementation("com.google.android.gms:play-services-maps:${libraries.gmsVersion}")
    //endregion

    // Jake Wharton libraries
    //timber
    implementation "com.jakewharton.timber:timber:${libraries.timber}"

    //GSON
    implementation 'com.google.code.gson:gson:2.8.2'

    //JOB SCHEDULER
    implementation 'com.firebase:firebase-jobdispatcher:0.8.5'
    implementation("android.arch.work:work-runtime:1.0.0-alpha10") {
        exclude group: 'com.google.guava', module: 'listenablefuture'

    }

    //CRASHLYTICS
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true
    }

    //FIREBASE
    implementation 'com.google.firebase:firebase-core:16.0.1'

    //Test

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'

    })

    testImplementation 'junit:junit:4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation "org.robolectric:robolectric:3.3.2"
    testImplementation group: 'org.mockito', name: 'mockito-all', version: '1.8.4'
    androidTestImplementation 'com.google.dagger:dagger:2.14.1'
    androidTestImplementation 'com.google.dagger:dagger-compiler:2.14.1'

    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'

    //RXLint
    implementation 'nl.littlerobots.rxlint:rxlint:1.6'

    //JODA-TIME
    implementation 'joda-time:joda-time:2.9.9'

    // Zxing library
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
    implementation 'com.journeyapps:zxing-android-embedded:3.2.0@aar'

    //FancyShowCase
    implementation 'com.github.faruktoptas:FancyShowCaseView:1.0.0'

    //Lottie animations
    implementation 'com.airbnb.android:lottie:2.5.4'

    //Glide load images
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'
    implementation "com.tbuonomo.andrui:viewpagerdotsindicator:${libraries.indicatordots}"

    //Paperwork
    implementation "hu.supercluster:paperwork:${libraries.paperwork}"

    implementation group: 'com.google.guava', name: 'guava', version: '24.1-jre'


}

apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'
