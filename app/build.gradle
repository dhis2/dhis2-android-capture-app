apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'hu.supercluster.paperwork'

def libraries = rootProject.ext.libraries
def versions = rootProject.ext.versions

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    mavenCentral()
}

paperwork {
    set = [
            buildTime: buildTime("yyyy-MM-dd HH:mm"),
            gitSha   : gitSha()
    ]
}

android {
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
        animationsDisabled = true
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    ndkVersion versions.ndk
    compileSdkVersion versions.sdk
    buildToolsVersion versions.tools

    defaultConfig {
        applicationId "com.dhis2"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.sdk
        versionCode versions.vCode
        versionName versions.vName
        testInstrumentationRunner "org.dhis2.Dhis2Runner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        setProperty("archivesBaseName", "dhis2-v" + versions.vName)

        def mapboxAccessToken = System.getenv("MAPBOX_ACCESS_TOKEN") != null ? System.getenv("MAPBOX_ACCESS_TOKEN") : "pk.eyJ1IjoiZGhpczJhbmRyb2lkIiwiYSI6ImNrcWt1a2hzYzE5Ymsyb254MWtlbGt4Y28ifQ.JrP61q9BFTVEKO4SwRUwDw"

        buildConfigField "String", "SDK_VERSION", "\"" + libraries.dhis2sdk + "\""
        buildConfigField "String", "MAPBOX_ACCESS_TOKEN", "\"" + mapboxAccessToken + "\""
        buildConfigField "String", "MATOMO_URL", "\"https://usage.analytics.dhis2.org/matomo.php\""
        buildConfigField("long", "VERSION_CODE", "${defaultConfig.versionCode}")
        buildConfigField("String","VERSION_NAME","\"${defaultConfig.versionName}\"")

        manifestPlaceholders = [
                appAuthRedirectScheme: ''
        ]
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        // excluding duplicate license files
        // from jackson modules
        exclude "META-INF/LICENSE"

        // excluding duplicate rxjava properties file
        // see https://github.com/ReactiveX/RxJava/issues/4445#issuecomment-243591879
        exclude 'META-INF/rxjava.properties'
    }

    buildTypes {

        def bitriseSentryDSN = System.getenv("SENTRY_DSN") != null ? System.getenv("SENTRY_DSN") : ""

        debug {
            // custom application suffix which allows to
            // install debug and release builds at the same time
            applicationIdSuffix = ".debug"

            // jacoco test coverage reports both for
            // androidTest and test source sets
            testCoverageEnabled = false

            // Using dataentry.jks to sign debug build type.
            signingConfig = signingConfigs.debug

            resValue "string", "content_provider", "com.dhis2.debug.provider"
            buildConfigField "int", "MATOMO_ID", "2"

            applicationVariants.all { variant ->
                if (variant.getFlavorName() == "dhisUITesting") {
                    bitriseSentryDSN = ""
                }
            }

            //Sentry variables
            manifestPlaceholders = [
                    sentry_dsn        : bitriseSentryDSN,
                    sentry_environment: "debug",
                    sentry_debug      : "true",
                    sentry_sample_rate: "1.0"
            ]
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "int", "MATOMO_ID", "1"
            resValue "string", "content_provider", "com.dhis2.provider"

            //Sentry variables
            manifestPlaceholders = [
                    sentry_dsn        : bitriseSentryDSN,
                    sentry_environment: "production",
                    sentry_debug      : "false",
                    sentry_sample_rate: "0.1"
            ]
        }
    }

    flavorDimensions "default"

    productFlavors {
        dhis {
            applicationId "com.dhis2"
            dimension "default"
            versionCode versions.vCode
            versionName versions.vName
        }

        dhisNoSMS {
            applicationId "com.dhis2"
            dimension "default"
            versionCode versions.vCode
            versionName versions.vName
        }

        dhisUITesting {
            applicationId "com.dhis2"
            dimension "default"
            versionCode versions.vCode
            versionName versions.vName
        }
    }

    compileOptions {
        targetCompatibility = "8"
        sourceCompatibility = "8"
    }

    buildFeatures {
        compose true
        dataBinding true
        viewBinding true
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/rxjava.properties'
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    configurations.all {
        resolutionStrategy {
            preferProjectModules()

            force 'junit:junit:4.12', 'com.squareup.okhttp3:okhttp:3.12.0'

            forcedModules = ['com.squareup.okhttp3:okhttp:3.12.0']
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.0.1'
    }
}

sonarqube {
    properties {
        property "sonar.language", "java"
    }
}

dependencies {
    implementation 'androidx.compose.material:material:1.0.4'
    implementation "com.google.android.material:compose-theme-adapter:1.0.4"
    implementation 'androidx.compose.ui:ui-tooling-preview:1.0.4'
    implementation 'androidx.activity:activity-compose:1.3.1'
    debugImplementation "androidx.compose.ui:ui-tooling:1.0.4"

    debugImplementation project(':dhis2-rule-engine')
    releaseImplementation("org.hisp.dhis.rules:rule-engine:${libraries.ruleEngine}") {
        exclude group: "junit", module: "junit"
    }

    implementation project(':viewpagerdotsindicator')
    implementation project(':tableview')
    implementation project(":dhis_android_analytics")
    implementation project(":form")
    implementation project(":commons")
    implementation project(":dhis2_android_maps")

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation "androidx.annotation:annotation:${libraries.annotation}"
    implementation "androidx.appcompat:appcompat:${libraries.appcompat}"
    implementation "androidx.cardview:cardview:${libraries.cardview}"
    implementation "androidx.legacy:legacy-support-v4:${libraries.legacysupport}"
    implementation "androidx.multidex:multidex:${libraries.multidex}"
    implementation "androidx.constraintlayout:constraintlayout:${libraries.constraintlayout}"
    implementation "androidx.lifecycle:lifecycle-extensions:${libraries.lifecycle}"
    implementation "androidx.work:work-runtime:${libraries.work}"
    implementation "androidx.work:work-rxjava2:${libraries.work}"
    implementation "androidx.exifinterface:exifinterface:${libraries.exif}"
    implementation "androidx.core:core-ktx:${libraries.corektx}"
    implementation "androidx.fragment:fragment-ktx:${libraries.fragmentktx}"
    implementation "androidx.recyclerview:recyclerview:${libraries.recyclerview}"

    //region UI LIBRARIES
    //Google
    implementation "com.google.android.material:material:${libraries.material}"
    implementation "com.google.android.flexbox:flexbox:${libraries.flexbox}"
    implementation("androidx.compose.ui:ui:1.0.4")
    implementation("androidx.compose.runtime:runtime-livedata:1.0.4")


    //AndroidTreeView
    implementation "com.github.bmelnychuk:atv:${libraries.atv}"

    //PIN LOCK
    implementation "com.andrognito.pinlockview:pinlockview:${libraries.pinlock}"

    //FancyShowCase
    implementation "me.toptas.fancyshowcase:fancyshowcaseview:${libraries.fancyshowcase}"

    //Lottie animations
    implementation "com.airbnb.android:lottie:${libraries.lottie}"
    //endregion

    //region DEPENDENCY INJECTION
    implementation "com.google.dagger:dagger:${libraries.dagger}"
    kapt "com.google.dagger:dagger-compiler:${libraries.dagger}"
    //endregion

    //region RXJAVA
    implementation "io.reactivex.rxjava2:rxandroid:${libraries.rxandroid}"
    implementation "io.reactivex.rxjava2:rxjava:${libraries.rxjava}"
    implementation "com.github.akarnokd:rxjava2-interop:${libraries.rxjavainteroperop}"
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    //rxbinding
    implementation "com.jakewharton.rxbinding2:rxbinding:${libraries.rxbindings}"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:${libraries.rxbindings}"
    //endregion

    // Extension which generates mappers for work with cursors
    kapt "com.gabrielittner.auto.value:auto-value-cursor:${libraries.autovaluecursor}"
    kapt "com.ryanharter.auto.value:auto-value-parcel:${libraries.autovalueparcel}"
    //endregion

    //region Analytics
    implementation "org.matomo.sdk:tracker:${libraries.matomo}"
    implementation "io.sentry:sentry-android:${libraries.sentry}"
    //endregion

    //region Debug
    //Timber
    implementation "com.jakewharton.timber:timber:${libraries.timber}"
    //Flipper
    debugImplementation "com.facebook.flipper:flipper:${libraries.flipper}"
    debugImplementation "com.facebook.soloader:soloader:${libraries.soloader}"
    debugImplementation "com.facebook.flipper:flipper-network-plugin:${libraries.flippernetwork}"
    debugImplementation "com.facebook.flipper:flipper-leakcanary-plugin:${libraries.flipperleak}"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:${libraries.leakcannary}"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${libraries.leakcannarynoop}"
    releaseImplementation "com.facebook.flipper:flipper-noop:${libraries.flippernoop}"
    //RXLint
    implementation "nl.littlerobots.rxlint:rxlint:${libraries.rxlint}"
    implementation "hu.supercluster:paperwork:${libraries.paperwork}"
    implementation "cat.ereza:customactivityoncrash:${libraries.crashactivity}"
    //endregion

    //region QR/Barcode scanner
    implementation "com.google.zxing:core:${libraries.zxing}"
    implementation "me.dm7.barcodescanner:zxing:${libraries.zxingbarcode}"
    implementation "com.journeyapps:zxing-android-embedded:${libraries.zxingandroid}"
    //endregion

    //region Security
    implementation "de.adorsys.android:securestoragelibrary:${libraries.latestSecureStorageVersion}"
    implementation "co.infinum:goldfinger-rx:${libraries.goldfinger}"
    implementation "com.scottyab:rootbeer-lib:${libraries.root}"
    implementation "net.openid:appauth:${libraries.openid}"
    //endregion

    //region Other
    implementation "com.google.code.gson:gson:${libraries.gson}"
    implementation "com.squareup.retrofit2:converter-gson:${libraries.gsonconverter}"
    implementation "com.squareup.okhttp3:okhttp:${libraries.okhttp}"
    implementation "joda-time:joda-time:${libraries.jodatime}"
    implementation "com.google.guava:guava:${libraries.guava}"
    //endregion

    //region Test
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.test.espresso:espresso-idling-resource:3.3.0'
    implementation 'androidx.test.espresso.idling:idling-concurrent:3.3.0'

    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "androidx.test:core:1.3.0"
    testImplementation "junit:junit:${libraries.junit}"
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'org.mockito:mockito-inline:2.13.0'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation 'com.google.truth:truth:0.43'

    androidTestUtil 'androidx.test:orchestrator:1.3.0'
    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-accessibility:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.3.0'
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    androidTestImplementation "androidx.test:core:1.3.0"
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation "androidx.test:core-ktx:1.3.0"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.2"
    androidTestImplementation 'org.mockito:mockito-android:2.28.2'
    androidTestImplementation 'org.mockito:mockito-core:3.4.6'
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'
    androidTestImplementation 'com.squareup.rx.idler:rx2-idler:0.11.0'
    //endregion
}