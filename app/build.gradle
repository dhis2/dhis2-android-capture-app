apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'hu.supercluster.paperwork'

def libraries = rootProject.ext.libraries
def versions = rootProject.ext.versions

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    mavenCentral()
}

paperwork {
    set = [
            buildTime: buildTime("yyyy-MM-dd HH:mm"),
            gitSha   : gitSha()
    ]
}

android {
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
        animationsDisabled = true
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileSdkVersion versions.sdk
    buildToolsVersion versions.tools
    defaultConfig {
        applicationId "com.dhis2"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.sdk
        versionCode versions.vCode
        versionName versions.vName
        testInstrumentationRunner "org.dhis2.Dhis2Runner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        setProperty("archivesBaseName", "dhis2-v" + versions.vName)

        def bitriseAcraUrl = System.getenv("ACRA_URL")
        def acraUrl = bitriseAcraUrl != null ? bitriseAcraUrl : "http://127.0.0.1:8080/report"

        buildConfigField "String", "SDK_VERSION", "\"" + libraries.dhis2sdk + "\""
        buildConfigField "String", "MAPBOX_ACCESS_TOKEN", "\"pk.eyJ1Ijoib3Jpa2dnIiwiYSI6ImNqdWpqb24zYTFsOXo0M2xwcXRseGx3aXkifQ.6N9aqvpqdrrSBwO9GyfjUw\""
        buildConfigField "String", "ACRA_URL", "\"$acraUrl\""
        buildConfigField "String", "MATOMO_URL", "\"https://usage.analytics.dhis2.org/matomo.php\""

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }

        manifestPlaceholders = [
                appAuthRedirectScheme: ''
        ]
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        // excluding duplicate license files
        // from jackson modules
        exclude "META-INF/LICENSE"

        // excluding duplicate rxjava properties file
        // see https://github.com/ReactiveX/RxJava/issues/4445#issuecomment-243591879
        exclude 'META-INF/rxjava.properties'
    }

    buildTypes {

        def bitriseSentryDSN = System.getenv("SENTRY_DSN") != null ? System.getenv("SENTRY_DSN") : ""

        debug {
            // custom application suffix which allows to
            // install debug and release builds at the same time
            applicationIdSuffix = ".debug"

            // jacoco test coverage reports both for
            // androidTest and test source sets
            testCoverageEnabled = false

            // Using dataentry.jks to sign debug build type.
            signingConfig = signingConfigs.debug

            def bitriseAcraUserDebug = System.getenv("ACRA_USER_DEBUG")
            def bitriseAcraPassDebug = System.getenv("ACRA_PASS_DEBUG")
            def acraUserDebug = bitriseAcraUserDebug != null ? bitriseAcraUserDebug : "empty"
            def acraPassDebug = bitriseAcraPassDebug != null ? bitriseAcraPassDebug : "empty"

            resValue "string", "content_provider", "com.dhis2.debug.provider"
            buildConfigField "String", "ACRA_USER", "\"$acraUserDebug\""
            buildConfigField "String", "ACRA_PASSWORD", "\"$acraPassDebug\""
            buildConfigField "int", "MATOMO_ID", "2"

            //Sentry variables
            manifestPlaceholders = [
                    sentry_dsn        : bitriseSentryDSN,
                    sentry_environment: "debug",
                    sentry_debug      : "true"
            ]
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            def bitriseAcraUserProd = System.getenv("ACRA_USER_PROD")
            def bitriseAcraPassProd = System.getenv("ACRA_PASS_PROD")
            def acraUserProd = bitriseAcraUserProd != null ? bitriseAcraUserProd : "empty"
            def acraPassProd = bitriseAcraPassProd != null ? bitriseAcraPassProd : "empty"

            buildConfigField "String", "ACRA_USER", "\"$acraUserProd\""
            buildConfigField "String", "ACRA_PASSWORD", "\"$acraPassProd\""
            buildConfigField "int", "MATOMO_ID", "1"
            resValue "string", "content_provider", "com.dhis2.provider"

            //Sentry variables
            manifestPlaceholders = [
                    sentry_dsn        : bitriseSentryDSN,
                    sentry_environment: "production",
                    sentry_debug      : "false"
            ]
        }
    }

    flavorDimensions "default"

    productFlavors {
        dhis {
            applicationId "com.dhis2"
            dimension "default"
            versionCode versions.vCode
            versionName versions.vName
        }

        dhisNoSMS {
            applicationId "com.dhis2"
            dimension "default"
            versionCode versions.vCode
            versionName versions.vName
        }

        dhisUITesting {
            applicationId "com.dhis2"
            dimension "default"
            versionCode versions.vCode
            versionName versions.vName
        }
    }

    compileOptions {
        targetCompatibility = "8"
        sourceCompatibility = "8"
    }

    buildFeatures {
        dataBinding true
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/rxjava.properties'
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    configurations.all {
        resolutionStrategy {
            preferProjectModules()

            force 'junit:junit:4.12', 'com.squareup.okhttp3:okhttp:3.12.0'

            forcedModules = ['com.squareup.okhttp3:okhttp:3.12.0']
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

sonarqube {
    properties {
        property "sonar.language", "java"
    }
}

dependencies {
    debugImplementation project(':core')
    releaseImplementation("org.hisp.dhis:android-core:${libraries.dhis2sdk}") {
        exclude group: "org.hisp.dhis", module: "core-rules"
    }

    debugImplementation project(':dhis2-rule-engine')
    releaseImplementation("org.hisp.dhis.rules:rule-engine:${libraries.ruleEngine}") {
        exclude group: "junit", module: "junit"
    }

    implementation project(':customFab')
    implementation project(':viewpagerdotsindicator')
    implementation project(':tableview')
    implementation project(":dhis_android_analytics")

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.multidex:multidex:${libraries.multidex}"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.lifecycle:lifecycle-extensions:${libraries.lifecycle}"
    implementation "androidx.work:work-runtime:2.2.0"
    implementation "androidx.work:work-rxjava2:2.2.0"
    implementation "androidx.exifinterface:exifinterface:1.2.0"

    implementation 'com.google.code.findbugs:jsr305:3.0.1'

    //region UI LIBRARIES
    //AndroidTreeView
    implementation "com.github.bmelnychuk:atv:${libraries.atv}"
    //FAB
    implementation "com.github.dekoservidoni:omfm:${libraries.omfm}"
    //FLEXBOX
    implementation "com.google.android:flexbox:${libraries.flexbox}"

    //PIN LOCK
    implementation 'com.andrognito.pinlockview:pinlockview:2.1.0'
    //endregion

    //region RXJAVA
    implementation "io.reactivex.rxjava2:rxandroid:${libraries.rxandroid}"
    implementation "io.reactivex.rxjava2:rxjava:${libraries.rxjava}"
    implementation "com.github.akarnokd:rxjava2-interop:${libraries.rxjavainteroperop}"
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'

    //rxbinding
    implementation "com.jakewharton.rxbinding2:rxbinding:${libraries.rxbindings}"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:${libraries.rxbindings}"

    //region DAGGER
    implementation "com.google.dagger:dagger:${libraries.dagger}"
    kapt "com.google.dagger:dagger-compiler:${libraries.dagger}"

    //region GOOGLE AUTO VALUE
    // Google's auto-value. Annotations are used only during compilation time, as well as
    // annotation processor which hoops up into compilation process
    api "com.google.auto.value:auto-value:${libraries.autovalue}"
    kapt "com.google.auto.value:auto-value:${libraries.autovalue}"

    // Extension which generates mappers for work with cursors
    kapt "com.gabrielittner.auto.value:auto-value-cursor:${libraries.autovaluecursor}"
    kapt "com.ryanharter.auto.value:auto-value-parcel:${libraries.autovalueparcel}"
    //endregion

    // Kujaku dependencies
    implementation 'io.ona.kujaku:library:0.9.0'
//    implementation 'com.mapbox.mapboxsdk:mapbox-android-sdk:9.2.0'
    implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-markerview-v8:0.3.0'
//    implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-annotation-v9:0.8.0'


    // Jake Wharton libraries
    //timber
    implementation "com.jakewharton.timber:timber:${libraries.timber}"

    //GSON
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'


    implementation 'org.matomo.sdk:tracker:4.1.2'
    implementation "ch.acra:acra-http:5.6.1"

    //FIREBASE
    implementation 'com.google.firebase:firebase-core:17.2.0'
    implementation 'com.google.firebase:firebase-perf:19.0.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "androidx.fragment:fragment-ktx:1.2.5"

    debugImplementation 'com.facebook.flipper:flipper:0.72.0'
    debugImplementation 'com.facebook.soloader:soloader:0.9.0'
    debugImplementation 'com.facebook.flipper:flipper-network-plugin:0.72.0'
    debugImplementation 'com.facebook.flipper:flipper-leakcanary-plugin:0.72.0'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.1'
    releaseImplementation 'com.facebook.flipper:flipper-noop:0.72.0'

    implementation 'com.squareup.okhttp3:okhttp:3.12.0'

    //RXLint
    implementation 'nl.littlerobots.rxlint:rxlint:1.6'

    //JODA-TIME
    implementation 'joda-time:joda-time:2.9.9'

    // Zxing library
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
    implementation 'com.journeyapps:zxing-android-embedded:3.2.0@aar'

    //FancyShowCase
    implementation 'me.toptas.fancyshowcase:fancyshowcaseview:1.1.5'

    //Lottie animations
    implementation 'com.airbnb.android:lottie:3.4.0'

    //Glide load images
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'

    //Paperwork
    implementation "hu.supercluster:paperwork:${libraries.paperwork}"
    implementation 'com.google.guava:guava:27.0.1-android'
    implementation 'cat.ereza:customactivityoncrash:2.3.0'
    implementation "com.google.android.gms:play-services-analytics:${libraries.analytics}"
    //implementation 'com.github.pwittchen:rxbiometric:0.1.0'
    implementation 'co.infinum:goldfinger-rx:2.0.1'
    implementation "de.adorsys.android:securestoragelibrary:${libraries.latestSecureStorageVersion}"
    implementation 'com.github.prabhat1707:DbExporterHelper:v1.0'

    /*region SECURITY*/
    implementation 'com.scottyab:rootbeer-lib:0.0.7'

    /*openId*/
    implementation 'net.openid:appauth:0.7.1'

    /*Sentry*/
    implementation "io.sentry:sentry-android:${libraries.sentry}"

    //Test
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.test.espresso:espresso-idling-resource:3.2.0'
    implementation 'androidx.test.espresso.idling:idling-concurrent:3.2.0'

    testImplementation "androidx.arch.core:core-testing:2.0.0"
    testImplementation "androidx.test:core:1.2.0"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.2.1'
    testImplementation "org.robolectric:shadows-multidex:4.0.1"
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'org.mockito:mockito-inline:2.13.0'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
    testImplementation 'com.google.truth:truth:0.43'

    androidTestUtil 'androidx.test:orchestrator:1.2.0'
    androidTestImplementation "androidx.test:runner:1.2.0"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-accessibility:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.2.0'
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    androidTestImplementation "androidx.test:core:1.2.0"
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation "androidx.test:core-ktx:1.2.0"
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.1"
    androidTestImplementation 'org.mockito:mockito-android:2.28.2'
    androidTestImplementation 'org.mockito:mockito-core:2.28.2'
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'
    androidTestImplementation 'com.squareup.rx.idler:rx2-idler:0.11.0'
}